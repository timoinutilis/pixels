let x=0
let y=0
do
	for dir=1 to 4
		for i=1 to 31
			cls 0
			color 4
			line y,x to x,y
			color 2
			line 31-y,31-x to x,y
			color 3
			line y,x to 31-y,31-x
			wait 0.05
			if dir=1 then let x=x+1
			if dir=2 then let y=y+1
			if dir=3 then let x=x-1
			if dir=4 then let y=y-1
		next i
	next dir
loop

--------------------------

let x=32
color 4
do
	cls 0
	let x=x-1
	text x,13,"inutilis software"
	wait 0.05
	if x<-70 then let x=32
loop

--------------------------

let carX=16
let carY=26
let streetY=0
let blockX=15
let blockY=0
let score=0
let blockIsBad=false
do
	if left(0) and carX>9 then let carX=carX-1
	if right(0) and carX<22 then let carX=carX+1

	if blockY>31 then let blockY=0
	if blockY=0 then
		let blockX=9+rnd*13
		let blockIsBad=false
		if rnd<0.3 then let blockIsBad=true
	end if
	
	cls 9
	color 3
	bar 8,0 to 23,31
	
	color 4
	line 7,0 to 7,31
	line 24,0 to 24,31
	color 1
	for i=0 to 7
		plot 7,streetY+i*4
		plot 24,streetY+i*4
	next
	let streetY=streetY+1
	if streetY=4 then let streetY=0
	
	color 8
	if blockIsBad then color 4
	let blockY=blockY+2
	box blockX-1,blockY to blockX+1,blockY+1
	
	let checkleft=point(carX-1,carY-1)
	let checkright=point(carX+1,carY-1)
	if checkleft=4 or checkleft=8 or checkright=4 or checkright=8 then
		let blockY=0
		if not blockIsBad then let score=score+1
		if blockIsBad then let score=0
	end if

	color 12
	bar carX-1,carY to carX+1,carY+3
	
	color 1
	text 16,1,score,1
	if score=20 then exit

	wait 0.05
loop

-------------------------

LET AY=15
LET BY=15
LET PDIRX=-1
LET ASCORE=0
LET BSCORE=0

DO
IF PDIRX=1 THEN
 LET PX=1
 LET PY=AY
END IF
IF PDIRX=-1 THEN
 LET PX=30
 LET PY=BY
END IF

LET PDIRY=RND*2-1

DO
 IF UP(0) AND AY>2 THEN LET AY=AY-1
 IF DOWN(0) AND AY<29 THEN LET AY=AY+1

 IF BY>PY AND BY>2 THEN LET BY=BY-1
 IF BY<PY AND BY<29 THEN LET BY=BY+1

 LET PX=PX+PDIRX
 LET PY=PY+PDIRY

 IF PY<=0 AND PDIRY<0 OR PY>=31 AND PDIRY>0 THEN LET PDIRY=-PDIRY
 IF PX<=1 AND PDIRX<0 THEN
   IF PY>AY-3 AND PY<AY+3THEN
     LET PDIRX=-PDIRX
     LET PDIRY=PDIRY+(PY-AY)/2
   END IF
   IF PX<0 THEN
     LET BSCORE=BSCORE+1
     EXIT
   END IF
 END IF
 IF PX>=30 AND PDIRX>0 THEN
   IF PY>BY-3 AND PY<BY+3 THEN
     LET PDIRX=-PDIRX
     LET PDIRY=PDIRY+(PY-BY)/2
   END IF
   IF PX>31 THEN
     LET ASCORE=ASCORE+1
     EXIT
   END IF
 END IF
 CLS 0
 COLOR 1
 LINE 0,AY-2 TO 0,AY+2
 LINE 31,BY-2 TO 31,BY+2
 PLOT PX,PY
 WAIT 0.05
LOOP

COLOR 1
TEXT 7,2,ASCORE,1
TEXT 23,2,BSCORE,1
WAIT 1

LOOP

-------------------------

LET D=1

DO
CLS 0

COLOR 1
BOX 0,0 TO 31,31
LET X=15
LET Y=15
LET SCORE=0

COLOR 3
PLOT 20,20

DO
 IF LEFT(0) AND D<>2 THEN LET D=1
 IF RIGHT(0) AND D<>1 THEN LET D=2
 IF UP(0) AND D<>4 THEN LET D=3
 IF DOWN(0) AND D<>3 THEN LET D=4
 IF D=1 THEN LET X=X-1
 IF D=2 THEN LET X=X+1
 IF D=3 THEN LET Y=Y-1
 IF D=4 THEN LET Y=Y+1
 IF POINT(X,Y)=1 THEN EXIT
 IF POINT(X,Y)=3 THEN
   LET SCORE=SCORE+1
   COLOR 3
   PLOT 1+RND*29,1+RND*29
 END IF
 COLOR 1
 PLOT X,Y
 WAIT 0.1
LOOP
COLOR 2
BOX 0,0 TO 31,31

TEXT 15,13,SCORE,1

REPEAT
 WAIT 0.1
UNTIL BUTTON(0)
LOOP
