REM ********************
REM LOWRES SNAKE 2.2
REM BY TIMO KLOSS
REM SNAKE GAME
REM COLLECT BLINKING DOTS AND DON'T HIT YOURSELF!
REM ********************

PERSIST HISCORE

MAXLEN=1000
DIM TX(MAXLEN),TY(MAXLEN)
BO=10

D=1

DO
  REM THE TITLE SCREEN

  GAMEPAD 0

  CLS 9
  COLOR 10
  FOR I=1 TO 20
    PLOT RND*64,RND*64
  NEXT I

  T$="LOWRES SNAKE 2"
  COLOR 10
  TEXT 31,11,T$,1
  COLOR 1
  TEXT 31,10,T$,1

  COLOR 1
  TEXT 32,33,"HI:"+HISCORE,1

  GOSUB PLAYMUSIC

  REPEAT
    GOSUB BLINK
    TEXT 31,40,"PRESS BUTTON",1
    WAIT 0.1
  UNTIL BUTTON TAP(0)

  REM THE GAME

  REM PAINTS GREEN SCREEN WITH WHITE BORDER
  CLS 10
  COLOR 9
  BAR BO,BO TO 63-BO,63-BO
  COLOR 1
  BOX BO,BO TO 63-BO,63-BO

  REM RESET GAME
  TLEN=8
  TI=0
  FOR I=0 TO MAXLEN
    TX(I)=0
  NEXT I
  X=31
  Y=31
  SCORE=0
  TIME=0

  GOSUB PLACEDOT
  GOSUB DRAWINFO

  GAMEPAD 1

  DO
    REM CHECKS FOR DIRECTION CHANGES
    IF LEFT(0) AND D<>2 THEN
      D=1
    ELSE IF RIGHT(0) AND D<>1 THEN
      D=2
    ELSE IF UP(0) AND D<>4 THEN
      D=3
    ELSE IF DOWN(0) AND D<>3 THEN
      D=4
    END IF

    REM MOVES SNAKE IN CURRENT DIRECTION
    IF D=1 THEN X=X-1
    IF D=2 THEN X=X+1
    IF D=3 THEN Y=Y-1
    IF D=4 THEN Y=Y+1

    REM CHECKS FOR COLLISION
    IF POINT(X,Y)=1 THEN EXIT

    REM CHECKS FOR COLLECT
    IF X=DX AND Y=DY THEN
      SCORE=SCORE+1
      GOSUB SNDCOLLECT
      TLEN=TLEN+8
      IF TLEN>=MAXLEN THEN TLEN=MAXLEN-1
      GOSUB PLACEDOT
    END IF

    REM DRAWS HEAD OF SNAKE AT CURRENT POSITION
    COLOR 1
    PLOT X,Y

    REM STORE CURRENT POSITION AND HANDLE TAIL
    TX(TI)=X
    TY(TI)=Y
    TEI=((TI-TLEN)+MAXLEN) MOD MAXLEN
    IF TX(TEI)<>0 THEN
      COLOR 9
      PLOT TX(TEI),TY(TEI)
    END IF
    TI=(TI+1) MOD MAXLEN

    REM DRAWS BLINKING DOT TO COLLECT
    GOSUB BLINK
    PLOT DX,DY

    GOSUB DRAWINFO

    WAIT 0.1
    TIME=TIME+0.1
  LOOP

  REM GAME OVER!

  GOSUB SNDHIT
  GAMEPAD 0

  IF SCORE>HISCORE THEN
    HISCORE=SCORE
    ENDTXT$="NEW HIGHSCORE"
  ELSE
    ENDTXT$="GAME OVER"
  END IF

  REPEAT
    GOSUB BLINK
    TEXT 31,58,ENDTXT$,1
    WAIT 0.1
  UNTIL BUTTON TAP(0)

LOOP

PLACEDOT:
REM SETS NEW POSITION FOR DOT
DX=INT(BO+1+RND*(61-2*BO))
DY=INT(BO+1+RND*(61-2*BO))
RETURN

BLINK:
REM TOGGLES BETWEEN COLORS
IF C=6 THEN C=7 ELSE C=6
COLOR C
RETURN

DRAWINFO:
COLOR 10
BAR 0,0 TO 63,BO-1
COLOR 1
TEXT BO,1,SCORE
S=INT(TIME)
M=INT(S/60)
S=S MOD 60
IF S<10 THEN D$=":0" ELSE D$=":"
TEXT 63-BO,1,M+D$+S,2
RETURN

PLAYMUSIC:
SOUND 0,53,12
SOUND 0,56,6
SOUND 0,49,6
SOUND 0,54,6
SOUND 0,53,6
SOUND 0,51,6
SOUND 0,53,6
SOUND 0,49,6
SOUND 0,0,6
SOUND 0,61,6
SOUND 1,37,12
SOUND 1,25,12
SOUND 1,32,12
SOUND 1,29,12
SOUND 1,25,6
RETURN

SNDCOLLECT:
FOR I=0 TO 2
  SOUND 2,50+I*5+SCORE MOD 5,2
  SOUND 2,46+I*6+SCORE MOD 5,2
NEXT I
RETURN

SNDHIT:
SOUND 0,56,6
SOUND 0,55,6
SOUND 0,54,6
SOUND 1,25,6
SOUND 1,24,6
SOUND 1,23,6
RETURN
