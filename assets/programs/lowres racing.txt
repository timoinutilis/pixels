REM ********************
REM LOWRES RACING 1.2
REM BY TIMO KLOSS
REM RACING GAME
REM COLLECT GREEN AND AVOID RED BOXES!
REM ********************

DIM NT(2,2)

REM MUSIC INSTRUMENTS
DEF SOUND 1,2,0.5
DEF SOUND 2,0
DEF SOUND 3,3,0,0.1
DEF SOUND 4,3,0,0.08

REM SOUND FX
DEF SOUND 10,2,0.5
DEF SOUND 11,2,0.3
DEF SOUND 12,2,0.1
DEF SOUND 13,1
DEF SOUND 14,3
DEF SOUND LINE 14,0.5,24,0

GAMEINTRO:

GAMEPAD 0

REM DRAWS TITLE BACKGROUND
COLOR 9
BAR 0,31 TO 63,63
COLOR 10
FOR I=1 TO 20
  PLOT RND*64,RND*31+31
NEXT I
COLOR 3
FOR Y=0 TO 32
  BAR 30-Y,31+Y TO 33+Y,31+Y
NEXT Y

RESTORE MUSIC
GOSUB PLAYSONG

TXT$="LOWRES RACING"
C=8
REPEAT
  COLOR 11
  BAR 0,0 TO 63,30

  COLOR 0
  TEXT 32,14,TXT$,1
  COLOR 1
  TEXT 31,13,TXT$,1

  REM RENDERS WAVE EFFECT ON TEXT
  SR=SR-0.2
  FOR SX=0 TO 63
    SCROLL SX,0 TO SX,30,0,SIN(SR+SX/64*PI)*12
  NEXT SX

  C=C-1
  IF C=5 THEN C=8
  COLOR C
  TEXT 31,58,"PRESS BUTTON",1

  WAIT 0.04
UNTIL BUTTON TAP(0)

SOUND OFF

START:

REM INITIALIZES GAME VARIABLES
CARX=32
CARY=52
STREETY=0
BLOCKX=30
BLOCKY=0
SCORE=0
BLOCKISBAD=FALSE
GAMEOVER=FALSE
LEVEL=1
LIVES=3
BCOL=4
GCOL=9

CLS 9
GAMEPAD 1

DO
  REM MOVES THE CAR
  IF LEFT(0) AND CARX>18 THEN CARX=CARX-2
  IF RIGHT(0) AND CARX<44 THEN CARX=CARX+2

  REM RESETS THE BLOCK WHEN REACHED BOTTOM
  IF BLOCKY>62 THEN BLOCKY=0
  IF BLOCKY=0 THEN
    BLOCKX=18+RND*26
    BLOCKISBAD=RND<0.3
  END IF

  REM SCROLLS AND DRAWS THE GREEN SIDES
  SCROLL 0,0 TO  15,63,0,2
  SCROLL 47,0 TO 63,63,0,2
  COLOR 9
  BAR 0,0 TO 15,1
  BAR 47,0 TO 63,1
  COLOR 10
  IF RND<0.2 THEN PLOT RND*16,0
  IF RND<0.2 THEN PLOT 47+RND*17,0

  REM DRAWS THE STREET
  COLOR 3
  BAR 16,0 TO 46,63

  REM DRAWS THE MOVING SIDE LINES
  COLOR 4
  LINE 15,0 TO 15,63
  LINE 47,0 TO 47,63
  COLOR 1
  FOR I=0 TO 7
    BAR 15,STREETY+I*8 TO 15,STREETY+I*8+2
    BAR 47,STREETY+I*8 TO 47,STREETY+I*8+2
  NEXT I
  STREETY=STREETY+2
  IF STREETY=8 THEN STREETY=0

  REM DRAWS THE BLOCK
  IF BLOCKISBAD THEN COLOR BCOL ELSE COLOR GCOL
  BLOCKY=BLOCKY+4
  BAR BLOCKX-2,BLOCKY TO BLOCKX+2,BLOCKY+3

  REM CHECKS BLOCK COLLISION WITH CAR
  P1=POINT(CARX-2,CARY-1)
  P2=POINT(CARX+2,CARY-1)
  IF P1=BCOL OR P1=GCOL OR P2=BCOL OR P2=GCOL THEN
    BLOCKY=0
    IF NOT BLOCKISBAD THEN
      SCORE=SCORE+1
      SOUND 1,60,2,13
      SOUND 1,67,2
      SOUND 1,72,2
    ELSE
      GAMEOVER=TRUE
    END IF
    IF SCORE MOD 5=0 THEN LEVEL=LEVEL+1
  END IF

  REM DRAWS THE CAR
  COLOR 13
  BAR CARX-2,CARY TO CARX+2,CARY+9
  COLOR 1
  PLOT CARX-1,CARY
  PLOT CARX+1,CARY
  BAR CARX-1,CARY+3 TO CARX+1,CARY+4
  COLOR 11
  BAR CARX-1,CARY+7 TO CARX+1,CARY+8

  COLOR 1
  TEXT 31,1,SCORE,1

  REM HANDLES COLLISION WITH RED BLOCK
  IF GAMEOVER THEN
    SOUND 1,20,6,14
    SOUND 0,1,0,10
    LIVES=LIVES-1
    IF LIVES>0 THEN
      FOR I=1 TO 6
        COLOR I MOD 2 + 4
        TEXT 31,30,"CRASH",1
        WAIT 0.1
      NEXT I
      GAMEOVER=FALSE
    ELSE
      EXIT
    END IF
  END IF

  SI=(SI+1) MOD 3
  SOUND 0,20+LEVEL,0,10+SI

  REM SPEED DEPENDS ON LEVEL
  DELAY=0.08-(LEVEL/100)
  IF DELAY<0.04 THEN DELAY=0.04
  WAIT DELAY
LOOP

REM GAME OVER!
GAMEPAD 0
SOUND 0,1,0,10
SOUND 1,14,6
C=8
REPEAT
  COLOR C
  C=C-1
  IF C=5 THEN C=8
  TEXT 31,20,"GAME OVER",1
  TEXT 31,40,"PRESS BUTTON",1
  WAIT 0.1
UNTIL BUTTON TAP(0)

GOTO GAMEINTRO

PLAYSONG:
TEMPO=4
IOFF=0
FOR I=0 TO 33
  FOR C=0 TO 2
    READ PTCH,INST
    IF PTCH>-1 THEN
      IF NT(C,2)>0 THEN
        SOUND C,NT(C,0),NT(C,2)*TEMPO,NT(C,1)
      END IF
      NT(C,0)=PTCH
      NT(C,1)=INST+IOFF
      NT(C,2)=1
    ELSE
      NT(C,2)=NT(C,2)+1
    END IF
  NEXT C
NEXT I
FOR C=0 TO 2
  IF NT(C,2)>0 THEN
    SOUND C,NT(C,0),NT(C,2)*TEMPO,NT(C,1)
  END IF
  NT(C,0)=0
  NT(C,1)=0
  NT(C,2)=0
NEXT C
RETURN

MUSIC:
DATA 58,1,27,2,25,4
DATA -1,0,39,2,-1,0
DATA -1,0,27,2,73,3
DATA -1,0,39,2,-1,0
DATA 54,1,27,2,25,4
DATA 51,1,39,2,-1,0
DATA -1,0,27,2,73,3
DATA 0,0,39,2,25,4
DATA 58,1,27,2,25,4
DATA -1,0,39,2,-1,0
DATA 0,0,27,2,73,3
DATA 49,1,39,2,-1,0
DATA -1,0,27,2,25,4
DATA 0,0,39,2,-1,0
DATA 51,1,27,2,73,3
DATA -1,0,39,2,-1,0
DATA 58,1,30,2,25,4
DATA -1,0,42,2,-1,0
DATA -1,0,30,2,73,3
DATA -1,0,42,2,-1,0
DATA 54,1,30,2,25,4
DATA 58,1,42,2,-1,0
DATA -1,0,30,2,73,3
DATA 0,0,42,2,25,4
DATA 56,1,32,2,25,4
DATA -1,0,44,2,-1,0
DATA 0,0,32,2,73,3
DATA 54,1,44,2,-1,0
DATA -1,0,32,2,25,4
DATA 0,0,44,2,-1,0
DATA 56,1,32,2,73,3
DATA -1,0,44,2,25,3
DATA 51,1,27,2,25,4
DATA -1,0,-1,0,-1,0
DATA 0,0,0,0,-1,0
