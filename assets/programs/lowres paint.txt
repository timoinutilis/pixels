REM ********************
REM LOWRES PAINT 1.0
REM BY TIMO KLOSS
REM GRAPHICS TOOL FOR CREATING FULL SCREEN IMAGES WITH FREEHAND, LINE, BOX, BAR AND FILL.
REM
REM PICTURE:
REM BUTTON A: HOLD TO DRAW
REM BUTTON B: GO TO MENU
REM MENU:
REM BUTTON A: BACK TO DRAW SCREEN WITH CURRENT SELECTIONS
REM BUTTON B: TOGGLE BETWEEN COLORS AND TOOLS
REM 
REM NO LOADING/SAVING OF IMAGES YET! (YOU CAN MAKE A SCREENSHOT BY PRESSING THE "HOME" AND "HOLD" BUTTONS)
REM ********************

GAMEPAD 1

REM READ SPRITES
DIM ARR(15)
FOR S=0 TO 5
  FOR I=0 TO 15
    READ ARR(I)
  NEXT I
  DEF SPRITE S,ARR
NEXT S

CX=32
CY=32
OX=-1
OY=-1
PC=1
CT=0
DMODE=0

MPICTURE:
PUT 0,0
DO
  REM MOVE CURSOR
  IF UP(0) THEN CY=(CY-1+64) MOD 64
  IF DOWN(0) THEN CY=(CY+1) MOD 64
  IF LEFT(0) THEN CX=(CX-1+64) MOD 64
  IF RIGHT(0) THEN CX=(CX+1) MOD 64

  CC=(CX<>OX OR CY<>OY)

  REM FLASHING CURSOR
  IF PC=1 THEN SC=2 ELSE SC=1
  IF CT<2 THEN
    SPRITE PALETTE 0,PC,SC
  ELSE
    SPRITE PALETTE 0,SC,PC
  END IF
  CT=(CT+1) MOD 4

  REM DRAWING
  ND=FALSE
  IF DMODE=4 THEN
    IF BUTTON TAP(0,1) THEN
      SPRITE OFF 0
      COLOR PC
      PAINT CX,CY
      ND=TRUE
    END IF
  ELSE IF BUTTON(0,1) THEN
    IF CC OR CSX=-1 THEN
      SPRITE OFF 0
      DR=(DMODE>0 AND DMODE<4)
      IF CSX=-1 THEN
        CSX=CX
        CSY=CY
        IF DR THEN GET 0,0 TO 63,63
      ELSE
        IF DR THEN PUT 0,0
      END IF
      COLOR PC
      IF DMODE=0 THEN
        PLOT CX,CY
      ELSE IF DMODE=1 THEN
        LINE CSX,CSY TO CX,CY
      ELSE IF DMODE=2 THEN
        BOX CSX,CSY TO CX,CY
      ELSE IF DMODE=3 THEN
        BAR CSX,CSY TO CX,CY
      END IF
    END IF
    ND=TRUE
  END IF
  IF NOT ND THEN
    SPRITE 0,CX-3,CY-3,0
    CSX=-1
  END IF
  OX=CX
  OY=CY

  IF BUTTON TAP(0,2) THEN
    SPRITE OFF
    GET 0,0 TO 63,63
    GOTO MMENU
  END IF

  WAIT 0.1 TAP
LOOP

MMENU:
GOSUB DRAWMENU
FOR I=0 TO 4
  SPRITE I,I*8,32,I+1
  SPRITE PALETTE I,1,2,3
NEXT I
BL=TRUE
DO
  IF MMODE=0 THEN
    REM SELECT COLOR
    IF UP(0) THEN PC=(PC+16-4) MOD 16
    IF DOWN(0) THEN PC=(PC+4) MOD 16
    IF LEFT(0) THEN PC=(PC+16-1) MOD 16
    IF RIGHT(0) THEN PC=(PC+1) MOD 16
  ELSE IF MMODE=1 THEN
    REM SELECT TOOL
    IF LEFT(0) THEN DMODE=(DMODE+5-1) MOD 5
    IF RIGHT(0) THEN DMODE=(DMODE+1) MOD 5
  END IF

  IF BUTTON(0,1) THEN
    REM WAIT UNTIL BUTTON RELEASE
    REPEAT
    UNTIL NOT BUTTON(0)
    CLS
    SPRITE OFF
    GOTO MPICTURE
  ELSE IF BUTTON TAP(0,2) THEN
    REM TOGGLE MENU SECTION
    MMODE=(MMODE+1) MOD 2
  END IF

  IF UP(0) OR DOWN(0) OR LEFT(0) OR RIGHT(0) THEN
    BL=TRUE
  ELSE
    BL=NOT BL
  END IF
  GOSUB DRAWMENU
  WAIT 0.2 TAP
LOOP

DRAWMENU:
CLS
I=0
FOR R=0 TO 3
  FOR C=0 TO 3
    COLOR I
    X=C*16
    Y=R*6
    BAR X+1,Y+1 TO X+14,Y+4
    IF I=PC AND (BL OR MMODE<>0) THEN
      COLOR 1
      BOX X,Y TO X+15,Y+5
    END IF
    I=I+1
  NEXT C
NEXT R
IF BL OR MMODE<>1 THEN
COLOR 3
X=DMODE*8
Y=32
BAR X,Y TO X+7,Y+7
END IF
RETURN


REM CURSOR
DATA 2,0,1,0,0,0,144,24,0,0,1,0,2,0,0,0

REM FREE HAND
DATA 0,0,5,0,16,64,0,64,1,4,4,4,1,80,0,0
REM LINE
DATA 0,0,16,0,4,0,1,0,0,64,0,16,0,4,0,0
REM BOX
DATA 0,0,21,84,16,4,16,4,16,4,16,4,21,84,0,0
REM FILLED BOX
DATA 0,0,21,84,21,84,21,84,21,84,21,84,21,84,0,0
REM FLOOD FILL
DATA 0,16,0,84,1,90,5,104,1,160,0,128,0,64,21,80