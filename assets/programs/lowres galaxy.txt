REM ********************
REM LOWRES GALAXY 1.2
REM BY TIMO KLOSS
REM SPACE SHOOTER GAME
REM ********************

PERSIST HISCORE

REM READ SPRITE DATA
DIM ARR(15)
FOR J=0 TO 4
  FOR I=0 TO 15
    READ ARR(I)
  NEXT I
  DEF SPRITE J,ARR
NEXT J

REM SET SPRITE COLORS
FOR I=1 TO 4
  SPRITE PALETTE I,8,7,6
NEXT I
FOR I=9 TO 15
  SPRITE PALETTE I,7
NEXT I

REM MELODY
DEF SOUND 0,1,0.5,0
DEF SOUND LINE 0,0.1,2,0
REM LASER
DEF SOUND 1,2,0.05,0
DEF SOUND LINE 1,0.2,24,1.0
REM EXPLO
DEF SOUND 2,3,0.5,0
DEF SOUND LINE 2,0.5,41,0
REM ALIEN LASER
DEF SOUND 3,1,0.5,0
DEF SOUND LINE 3,0.5,-24,0

REM TITLE SONG SOUNDS
DEF SOUND 11,0
DEF SOUND LINE 11,0.05,12,0
DEF SOUND 12,1,0,0.1
DEF SOUND LINE 12,0.025,12,0
DEF SOUND 13,2,0.2
DEF SOUND LINE 13,1,0,0.8
DEF SOUND 14,3,0,0.1
DEF SOUND LINE 14,0.1,24,0
DEF SOUND 15,2,0.5,0.1
DEF SOUND LINE 15,0.1,36,0

DIM ALN(3,1),BUL(7,1)
DIM NT(2,2)

REM TITLE SCREEN

START:
GAMEPAD 0
LAYER 1
CLS 0
T$="LOWRES GALAXY"
COLOR 5
TEXT 30,29,T$,1
TEXT 32,29,T$,1
TEXT 31,28,T$,1
TEXT 31,30,T$,1
COLOR 4
TEXT 31,29,T$,1
COLOR 12
TEXT 32,51,"HI:"+HISCORE,1
LAYER 0
A=0
RESTORE MUSIC
GOSUB PLAYSONG
REPEAT
  GOSUB BGSCROLL
  LAYER 1
  IF A MOD 20<10 THEN COLOR 1 ELSE COLOR 3
  TEXT 31,57,"PRESS BUTTON",1
  A=A+1
  LAYER 0
  WAIT 0.05
UNTIL BUTTON TAP(0)
SOUND OFF

REM GAME INIT

LIV=5
LEV=1
SCO=0
LTI=0

GOSUB HUD
FOR I=0 TO 3
  GOSUB INIALIEN
NEXT I
GOSUB INISHIP
GOSUB SNDLEVEL
GAMEPAD 1

DO
  REM CHECK LEVEL UP
  IF LTI>=10 THEN
    LEV=LEV+1
    LTI=0
    COLOR 15
    TEXT 32,0,"LEVEL "+LEV,1
    GOSUB SNDLEVEL
  END IF

  REM PLAYER'S SHIP
  IF CRA>0 THEN
    REM CRASH EFFECT
    CRA=CRA+1
    SPRITE PALETTE 0,RND*16,RND*16,RND*16
    IF CRA=16 THEN
      LIV=LIV-1
      GOSUB HUD
      IF LIV=0 THEN EXIT ELSE GOSUB INISHIP
    END IF
  ELSE
    REM SHIP CONTROL
    IF LEFT(0) AND SX>0 THEN SX=SX-1
    IF RIGHT(0) AND SX<63-5 THEN SX=SX+1
    IF UP(0) AND SY>0 THEN SY=SY-1
    IF DOWN(0) AND SY<63-7 THEN SY=SY+1
    SPRITE PALETTE 0,,RND*16
    IF CRA<0 THEN CRA=CRA+1
  END IF

  REM FLICKER SHIP IF PLAYER HAS SHIELD
  IF CRA>=0 OR CRA MOD 2=0 THEN
    SPRITE 0,SX,SY
  ELSE
    SPRITE OFF 0
  END IF

  REM PLAYER SHOOTING
  IF BUTTON TAP(0) AND CRA<=0 THEN
    SPRITE 1+LI,SX+2+RND*2,SY,1
    LI=(LI+1)MOD 4
    SOUND 1,38,2,1
  END IF

  REM MOVE PLAYER'S LASERS
  FOR I=1 TO 4
    IF SPRITE I(I)<>-1 THEN SPRITE I,,SPRITE Y(I)-4
  NEXT I

  REM UPDATE ALIENS
  FOR I=0 TO 3
    X=SPRITE X(5+I)
    Y=SPRITE Y(5+I)
    IF ALN(I,0)<0 THEN
      REM CRASH EFFECT
      SPRITE PALETTE 5+I,RND*16,RND*16,RND*16
      ALN(I,0)=ALN(I,0)-1
      IF ALN(I,0)=-8 THEN GOSUB INIALIEN
    ELSE IF SPRITE HIT(5+I,1 TO 4) THEN
      REM HIT BY LASER
      ALN(I,0)=-1
      SPRITE 5+I,X-2,Y-2,3
      SOUND 0,20,5,2
      SCO=SCO+40+10*LEV
      GOSUB HUD
      SPRITE OFF HIT
    ELSE
      REM MOVE
      IF ALN(I,0) MOD 8 = 7 THEN ALN(I,1)=INT(RND*3)
      ALN(I,0)=ALN(I,0)+1
      D=ALN(I,1)
      IF D=0 AND Y>=64 THEN D=-1
      IF D=1 AND X<=0 THEN D=2
      IF D=2 AND X>=59 THEN D=1
      IF D=0 THEN
        Y=Y+1
      ELSE IF D=1 THEN
        X=X-1
      ELSE IF D=2 THEN
        X=X+1
      END IF
      SPRITE 5+I,X,Y,2
      IF D=-1 THEN GOSUB INIALIEN ELSE ALN(I,1)=D

      REM ALIEN SHOOTING
      R=60-LEV
      IF R<20 THEN R=20
      IF Y<32 AND ALN(I,0) MOD R=0 THEN
        SPRITE 9+BI,X+1,Y+2,4
        U=SX-X
        V=SY-Y
        W=SQR(U*U+V*V)/2
        BUL(BI,0)=U/W
        BUL(BI,1)=V/W
        BI=(BI+1)MOD 7
        SOUND 2,84,3,3
      END IF
    END IF
  NEXT I

  REM MOVE ALIENS' BULLETS
  FOR I=0 TO 6
    IF BUL(I,1)<>0 THEN
      J=9+I
      SPRITE J,SPRITE X(J)+BUL(I,0),SPRITE Y(J)+BUL(I,1)
    END IF
  NEXT I

  REM CHECK PLAYER COLLISION
  IF SPRITE HIT(0,5 TO 15) AND CRA=0 THEN
    CRA=1
    SOUND 0,36,20,2
    SPRITE 0,,,3
  END IF

  GOSUB BGSCROLL

  WAIT 0.05
  LTI=LTI+0.05
LOOP

REM GAME OVER
GAMEPAD 0
SPRITE OFF
LAYER 1
COLOR 4
TEXT 31,29,"GAME OVER",1
IF SCO>HISCORE THEN
  HISCORE=SCO
  NEWHI=TRUE
ELSE
  NEWHI=FALSE
END IF
LAYER 0
IF NEWHI THEN
  FOR A=58 TO 62
    SOUND 2,A-7,4,0
    SOUND 2,A-2,4
    SOUND 2,A,4
  NEXT A
ELSE
  FOR A=62 TO 58 STEP -1
    SOUND 2,A,4,0
    SOUND 2,A-2,4
    SOUND 2,A-7,4
  NEXT A
END IF
A=15
REPEAT
  GOSUB BGSCROLL
  IF NEWHI THEN
    LAYER 1
    COLOR 1+RND*15
    TEXT 31,35,"NEW HIGHSCORE",1
    LAYER 0
  END IF
  A=A-1
  WAIT 0.05
UNTIL BUTTON TAP(0) AND A<=0
SOUND OFF
GOTO START

INISHIP:
CRA=-32
SX=29
SY=48
SPRITE PALETTE 0,4,5,11
SPRITE 0,SX,SY,0
RETURN

INIALIEN:
ALN(I,0)=INT(RND*60)
ALN(I,1)=0
SPRITE PALETTE 5+I,4+RND*12,1,4+RND*12
SPRITE 5+I,INT(RND*60),-4,2
RETURN

SNDLEVEL:
SOUND 2,50,5,0
SOUND 2,54,5
SOUND 2,57,5
SOUND 2,62,10
RETURN

HUD:
LAYER 1
CLS 0
COLOR 4
TEXT 0,0,LIV
COLOR 1
TEXT 63,0,SCO,2
LAYER 0
RETURN

BGSCROLL:
SCROLL 0,0 TO 63,63,0,1
COLOR 0
LINE 0,0 TO 63,0
IF RND<0.5 THEN
COLOR 12+RND*2
PLOT RND*64,0
END IF
RETURN

PLAYSONG:
MTEMPO=3
IOFF=10
FOR I=0 TO 63
  FOR C=0 TO 2
    READ PTCH,INST
    IF PTCH>-1 THEN
      IF NT(C,2)>0 THEN
        SOUND C,NT(C,0),NT(C,2)*MTEMPO,NT(C,1)
      END IF
      NT(C,0)=PTCH
      NT(C,1)=INST+IOFF
      NT(C,2)=1
    ELSE
      NT(C,2)=NT(C,2)+1
    END IF
  NEXT C
NEXT I
FOR C=0 TO 2
  IF NT(C,2)>0 THEN
    SOUND C,NT(C,0),NT(C,2)*MTEMPO,NT(C,1)
  END IF
  NT(C,0)=0
  NT(C,1)=0
  NT(C,2)=0
NEXT C
RETURN

REM SPACE SHIP
DATA 5,0,5,0,5,0,15,0,69,16,85,80,85,80,74,16
REM LASER
DATA 64,0,64,0,64,0,128,0,128,0,192,0,0,0,0,0
REM ALIEN
DATA 21,0,102,64,85,64,51,0,0,0,0,0,0,0,0,0
REM EXPLOSION
DATA 192,12,34,32,7,64,45,224,7,64,34,32,192,12,0,0
REM ALIEN BULLET
DATA 80,0,80,0,0,0,0,0,0,0,0,0,0,0,0,0

MUSIC:
DATA 25,5,58,3,70,2
DATA -1,0,-1,0,65,2
DATA 34,1,53,3,61,2
DATA -1,0,-1,0,-1,0
DATA 25,5,61,3,61,4
DATA -1,0,-1,0,-1,0
DATA 34,1,53,3,70,2
DATA -1,0,-1,0,65,2
DATA 25,5,65,3,-1,0
DATA -1,0,-1,0,61,4
DATA 34,1,53,3,-1,0
DATA -1,0,-1,0,61,2
DATA 25,5,61,3,61,4
DATA -1,0,-1,0,-1,0
DATA 34,1,60,3,70,2
DATA -1,0,-1,0,65,2
DATA 25,5,-1,0,61,2
DATA -1,0,-1,0,-1,0
DATA 30,1,61,3,66,2
DATA -1,0,-1,0,65,2
DATA 25,5,58,3,61,4
DATA -1,0,-1,0,-1,0
DATA 30,1,-1,0,61,2
DATA -1,0,-1,0,-1,0
DATA 25,5,53,3,66,2
DATA -1,0,-1,0,61,4
DATA 29,1,-1,0,-1,0
DATA -1,0,-1,0,68,2
DATA 25,5,0,0,61,4
DATA -1,0,-1,0,-1,0
DATA 25,5,-1,0,70,2
DATA -1,0,-1,0,65,2
DATA 25,5,65,3,61,2
DATA -1,0,-1,0,-1,0
DATA 34,1,58,3,68,2
DATA -1,0,-1,0,70,2
DATA 25,5,68,3,61,4
DATA -1,0,-1,0,-1,0
DATA 34,1,58,3,65,2
DATA -1,0,-1,0,61,4
DATA 25,5,53,3,-1,0
DATA -1,0,-1,0,70,2
DATA 34,1,58,3,61,4
DATA -1,0,-1,0,-1,0
DATA 25,5,66,3,61,4
DATA -1,0,-1,0,70,2
DATA 34,1,65,3,65,2
DATA -1,0,-1,0,61,2
DATA 25,5,-1,0,-1,0
DATA -1,0,-1,0,68,2
DATA 37,1,66,3,-1,0
DATA -1,0,-1,0,65,2
DATA 25,5,65,3,61,4
DATA -1,0,-1,0,68,2
DATA 37,1,-1,0,-1,0
DATA -1,0,-1,0,61,4
DATA 25,5,61,3,65,2
DATA -1,0,-1,0,68,2
DATA 39,1,-1,0,70,2
DATA -1,0,-1,0,0,0
DATA 0,0,0,0,65,2
DATA -1,0,-1,0,68,2
DATA -1,0,-1,0,70,2
DATA -1,0,-1,0,-1,0
