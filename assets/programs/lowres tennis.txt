REM ********************
REM LOWRES TENNIS 1.1
REM BY TIMO KLOSS
REM CLASSIC TENNIS VIDEO GAME
REM NOTE: SECOND PLAYER CONTROLS WITH A AND B BUTTONS
REM ********************

GAMEPAD 1
NPLAYERS=1

MENU:
SOUND 0,40,4,2
SOUND 0,44,4
SOUND 0,47,4
SOUND 0,52,4

X=31
D=1
REPEAT
  IF UP(0) THEN NPLAYERS=1
  IF DOWN(0) THEN NPLAYERS=2

  CLS 0

  REM DRAWS MOVING TITLE TEXT
  X=X+D
  IF X>=36 OR X<=26 THEN D=-D
  TEXT X,4,"LOWRES TENNIS",1

  REM DRAWS PLAYER MENU
  REM CURRENT SELECTION BLINKS
  IF NPLAYERS=2 OR B THEN TEXT 31,35,"1 PLAYER",1
  IF NPLAYERS=1 OR B THEN TEXT 31,41,"2 PLAYERS",1
  B=NOT B

  WAIT 0.1
UNTIL BUTTON TAP(0)

REM INITIALIZES GAME VARIABLES
Y1=30
Y2=30
PDIRX=-1
SCORE1=0
SCORE2=0

DO
  REM SETS BALL START POSITION FOR LEFT OR RIGHT PLAYER
  IF PDIRX=1 THEN
    PX=2
    PY=Y1
  ELSE
    PX=60
    PY=Y2
  END IF

  REM RANDOM BALL ANGLE
  PDIRY=RND*2-1

  SOUND 0,50,2,2

  DO
    REM PLAYER 1 CONTROL
    IF UP(0) AND Y1>4 THEN Y1=Y1-2
    IF DOWN(0) AND Y1<58 THEN Y1=Y1+2

    REM PLAYER 2 CONTROL BY USER OR CPU
    IF NPLAYERS=1 THEN
      IF Y2>PY AND Y2>4 THEN Y2=Y2-2
      IF Y2<PY AND Y2<58 THEN Y2=Y2+2
    ELSE
      IF BUTTON(0,2) AND Y2>4 THEN Y2=Y2-2
      IF BUTTON(0,1) AND Y2<58 THEN Y2=Y2+2
    END IF

    REM MOVES BALL POSITION
    PX=PX+PDIRX*2
    PY=PY+PDIRY*2

    REM BALL BOUNCES OFF FROM UPPER AND LOWER BORDERS
    IF PY<=0 AND PDIRY<0 OR PY>=62 AND PDIRY>0 THEN
      PDIRY=-PDIRY
      SOUND 0,20,1,2
    END IF
    IF PY<0 THEN PY=0
    IF PY>62 THEN PY=62

    REM CHECKS BALL COLLISION FOR PLAYER 1
    IF PX<=2 AND PDIRX<0 THEN
      IF PY>Y1-5 AND PY<Y1+5 THEN
        PDIRX=1
        PDIRY=PDIRY+(PY-Y1)/4
        SOUND 0,44,1,2
      END IF
      IF PX<0 THEN
        SCORE2=SCORE2+1
        EXIT
      END IF
    END IF

    REM CHECKS BALL COLLISION FOR PLAYER 2
    IF PX>=60 AND PDIRX>0 THEN
      IF PY>Y2-5 AND PY<Y2+5 THEN
        PDIRX=-1
        PDIRY=PDIRY+(PY-Y2)/4
        SOUND 0,45,1,2
      END IF
      IF PX>63 THEN
        SCORE1=SCORE1+1
        EXIT
      END IF
    END IF

    CLS 0

    REM DRAWS PLAYER 1
    BAR 0,Y1-4 TO 1,Y1+5

    REM DRAWS PLAYER 2
    BAR 62,Y2-4 TO 63,Y2+5

    REM DRAWS BALL
    BAR PX,PY TO PX+1,PY+1

    WAIT 0.05
  LOOP

  REM BALL OUT!

  SOUND 0,40,4,2
  SOUND 0,36,6

  REM SHOWS SCORES
  COLOR 1
  TEXT 15,4,SCORE1,1
  TEXT 47,4,SCORE2,1
  WAIT 1

  REM CHECKS FOR END OF MATCH AND SHOWS WINNER
  IF SCORE1=7 OR SCORE2=7 THEN
    IF SCORE1>SCORE2 THEN X=15 ELSE X=47
    TEXT X,10,"WINNER",1
    WAIT 2
    GOTO MENU
  END IF

LOOP
