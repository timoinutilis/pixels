REM RACING GAME
REM COLLECT GREEN AND AVOID RED BOXES!

GAMEPAD 1

GAMEINTRO:

REM DRAWS TITLE BACKGROUND
COLOR 9
BAR 0,31 TO 63,63
COLOR 10
FOR I=1 TO 20
  PLOT RND*64,RND*31+31
NEXT I
COLOR 3
FOR Y=0 TO 32
  BAR 30-Y,31+Y TO 33+Y,31+Y
NEXT Y

TXT$="LOWRES RACING"
C=8

REPEAT
  COLOR 11
  BAR 0,0 TO 63,30

  COLOR 0
  TEXT 32,14,TXT$,1
  COLOR 1
  TEXT 31,13,TXT$,1

  REM RENDERS WAVE EFFECT ON TEXT
  SR=SR-0.2
  FOR SX=0 TO 63
    SCROLL SX,0 TO SX,30,0,SIN(SR+SX/64*PI)*12
  NEXT SX

  C=C-1
  IF C=5 THEN C=8
  COLOR C
  TEXT 31,58,"PRESS BUTTON",1

  WAIT 0.04
UNTIL BUTTON(0)

REM WAITS FOR RELEASING THE BUTTON
WHILE BUTTON(0)
WEND

START:

REM INITIALIZES GAME VARIABLES
CARX=32
CARY=52
STREETY=0
BLOCKX=30
BLOCKY=0
SCORE=0
BLOCKISBAD=FALSE
GAMEOVER=FALSE
LEVEL=1
LIVES=3
BCOL=4
GCOL=9

CLS 9

DO
  REM MOVES THE CAR
  IF LEFT(0) AND CARX>18 THEN CARX=CARX-2
  IF RIGHT(0) AND CARX<44 THEN CARX=CARX+2

  REM RESETS THE BLOCK WHEN REACHED BOTTOM
  IF BLOCKY>62 THEN BLOCKY=0
  IF BLOCKY=0 THEN
    BLOCKX=18+RND*26
    BLOCKISBAD=RND<0.3
  END IF

  REM SCROLLS AND DRAWS THE GREEN SIDES
  SCROLL 0,0 TO  15,63,0,2
  SCROLL 47,0 TO 63,63,0,2
  COLOR 9
  BAR 0,0 TO 15,1
  BAR 47,0 TO 63,1
  COLOR 10
  IF RND<0.2 THEN PLOT RND*16,0
  IF RND<0.2 THEN PLOT 47+RND*17,0

  REM DRAWS THE STREET
  COLOR 3
  BAR 16,0 TO 46,63

  REM DRAWS THE MOVING SIDE LINES
  COLOR 4
  LINE 15,0 TO 15,63
  LINE 47,0 TO 47,63
  COLOR 1
  FOR I=0 TO 7
    BAR 15,STREETY+I*8 TO 15,STREETY+I*8+2
    BAR 47,STREETY+I*8 TO 47,STREETY+I*8+2
  NEXT I
  STREETY=STREETY+2
  IF STREETY=8 THEN STREETY=0

  REM DRAWS THE BLOCK
  IF BLOCKISBAD THEN COLOR BCOL ELSE COLOR GCOL
  BLOCKY=BLOCKY+4
  BAR BLOCKX-2,BLOCKY TO BLOCKX+2,BLOCKY+3

  REM CHECKS BLOCK COLLISION WITH CAR
  P1=POINT(CARX-2,CARY-1)
  P2=POINT(CARX+2,CARY-1)
  IF P1=BCOL OR P1=GCOL OR P2=BCOL OR P2=GCOL THEN
    BLOCKY=0
    IF NOT BLOCKISBAD THEN SCORE=SCORE+1 ELSE GAMEOVER=TRUE
    IF SCORE MOD 5=0 THEN LEVEL=LEVEL+1
  END IF

  REM DRAWS THE CAR
  COLOR 13
  BAR CARX-2,CARY TO CARX+2,CARY+9
  COLOR 1
  PLOT CARX-1,CARY
  PLOT CARX+1,CARY
  BAR CARX-1,CARY+3 TO CARX+1,CARY+4
  COLOR 11
  BAR CARX-1,CARY+7 TO CARX+1,CARY+8

  COLOR 1
  TEXT 31,1,SCORE,1

  REM HANDLES COLLISION WITH RED BLOCK
  IF GAMEOVER THEN
    LIVES=LIVES-1
    IF LIVES>0 THEN
      FOR I=1 TO 6
        COLOR I MOD 2 + 4
        TEXT 31,30,"CRASH",1
        WAIT 0.1
      NEXT I
      GAMEOVER=FALSE
    ELSE
      EXIT
    END IF
  END IF

  REM SPEED DEPENDS ON LEVEL
  DELAY=0.08-(LEVEL/100)
  IF DELAY<0.04 THEN DELAY=0.04
  WAIT DELAY
LOOP

REM GAME OVER!
C=8
REPEAT
  COLOR C
  C=C-1
  IF C=5 THEN C=8
  TEXT 31,20,"GAME OVER",1
  TEXT 31,40,"PRESS BUTTON",1
  WAIT 0.1
UNTIL BUTTON(0)

WHILE BUTTON(0)
WEND

GOTO GAMEINTRO
